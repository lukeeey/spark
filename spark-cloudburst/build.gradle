plugins {
    id 'net.kyori.blossom' version '1.1.0'
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

dependencies {
    compile project(':spark-common')
    compile('net.kyori:adventure-text-serializer-legacy:4.4.0')
    compile('com.google.code.gson:gson:2.8.0')
    compileOnly 'org.cloudburstmc:cloudburst-server:0.0.1-SNAPSHOT'
}

repositories {
    maven { url "https://repo.opencollab.dev/maven-snapshots/" }
}

blossom {
    replaceTokenIn('src/main/java/me/lucko/spark/cloudburst/CloudSparkPlugin.java')
    replaceToken '@version@', project.pluginVersion
    replaceToken '@desc@', project.pluginDescription
}

shadowJar {
    archiveName = 'spark-cloudburst.jar'

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}